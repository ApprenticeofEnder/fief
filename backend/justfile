set dotenv-load := false

default:
  @just --list

install:
  pip install -U -r requirements.dev.txt

lint:
	isort . && black . && mypy fief/ && tryceratops fief/

test $UNIT_TESTS="1" $TEST_DATABASE_URL="postgresql://fief:fiefpassword@localhost:5432/fief-test":
  pytest --cov fief/ --cov-report=term-missing

create-db:
	docker volume create fief-dev-db
	docker run -p 5432:5432 --name fief-dev-db -e PGDATA=/var/lib/postgresql/data/pgdata -e POSTGRES_USER=fief -e POSTGRES_PASSWORD=fiefpassword -v fief-dev-db:/var/lib/postgresql/data -d postgres:alpine

start-db:
  docker start fief-dev-db

stop-db:
  docker rm -f fief-dev-db

revision-global-db message: start-db
  alembic -n global revision --autogenerate -m "{{message}}"

migrate-global-db: start-db
  alembic -n global upgrade head

revision-account-db message: start-db
  alembic -n account revision --autogenerate -m "{{message}}"

migrate-account-db: start-db
  alembic -n account upgrade head

start: start-db
  uvicorn --host 0.0.0.0 --port 8000 --reload fief.app:app
